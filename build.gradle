buildscript {
    repositories {
        // mavenLocal()
        jcenter()
        maven {
            url "http://xbib.org/repository"
        }
    }
    dependencies {
        classpath 'org.xbib.gradle.plugin:gradle-plugin-jflex:1.1.0'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.2"
    id 'ivy-publish'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'idea'
apply plugin: 'org.xbib.gradle.plugin.jflex'

group = 'com.bertramlabs.plugins'
version = '0.1.8'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    compile("commons-logging:commons-logging:1.1.1")
    testCompile "org.codehaus.groovy:groovy-all:2.4.1"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}


publishing {
    repositories {
        ivy {
            url = "http://artifactory.embotics.com:8081/artifactory/libs-release-local"
			authentication {
	            basic(BasicAuthentication)
	        }            
            credentials(PasswordCredentials) {
	            username "publish"
	            password "Secret1"
	        }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId 'hcl4j'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'hcl4j'
                    description 'Hashicorp Configuration Language (HCL) Java Parser'
                    url 'https://github.com/bertramdev/hcl4j'
                    scm {
                        url 'https://github.com/bertramdev/hcl4j'
                        connection 'scm:https://bertramdev@github.com/bertramdev/hcl4j.git'
                        developerConnection 'scm:git://github.com/bertramdev/hcl4j.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
		ivy(IvyPublication) {
            organisation = 'com.embotics'
            descriptor.status = 'milestone'
            descriptor.branch = 'master'

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }        
    }
}


//Define bintrayUser and bintrayKey in ~/.gradle/gradle.properties
bintray {
    if(project.hasProperty('bintrayUser')) {
        user = bintrayUser
        key = bintrayKey
    }
    publications = ['maven']
    pkg {
        repo = 'gomorpheus'
        userOrg = 'bertramlabs'
        name = 'hcl4j'
        vcsUrl = 'https://github.com/bertramdev/hcl4j.git'
        licenses = ['Apache-2.0']
    }
}



task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task morpheusJavaDoc(type: Javadoc) {
  source = sourceSets.main.allJava
  title = "Hcl4j Docs"
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}


bintrayUpload.dependsOn build, sourcesJar, javadocJar
